group 'io.flutter.plugins.firebase.core'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.1'
    }
}

apply plugin: 'com.android.library'
apply from: file("../../../../gradle/global-config.gradle")


def getRootProjectExtOrDefaultProperty(name) {
    if (!rootProject.ext.has("FlutterFire")) return project.properties[name]
    if (!rootProject.ext.get("FlutterFire")[name]) return project.properties[name]
    return rootProject.ext.get("FlutterFire").get(name)
}

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
      namespace 'io.flutter.plugins.firebase.core'
    }

    compileSdk project.ext.compileSdk

    defaultConfig {
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility project.ext.javaVersion
        targetCompatibility project.ext.javaVersion
    }

    buildFeatures {
        buildConfig true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    dependencies {
        implementation platform("com.google.firebase:firebase-bom:${getRootProjectExtOrDefaultProperty("FirebaseSDKVersion")}")
        implementation "com.google.firebase:firebase-common"

        implementation 'androidx.annotation:annotation:1.7.0'
    }
}

apply from: file("./user-agent.gradle")
