group 'io.flutter.plugins.firebase.core'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.2'
    }
}

apply plugin: 'com.android.library'

def getRootProjectExtOrDefaultProperty(name) {
    if (!rootProject.ext.has("FlutterFire")) return project.properties[name]
    if (!rootProject.ext.get("FlutterFire")[name]) return project.properties[name]
    return rootProject.ext.get("FlutterFire").get(name)
}

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
      namespace 'io.flutter.plugins.firebase.core'
    }

    compileSdk 35

    defaultConfig {
        minSdk 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    dependencies {
        implementation platform("com.google.firebase:firebase-bom:${getRootProjectExtOrDefaultProperty("FirebaseSDKVersion")}")
        implementation "com.google.firebase:firebase-common"

        implementation 'androidx.annotation:annotation:1.7.0'
    }
}

apply from: file("./user-agent.gradle")
