group 'io.flutter.plugins.firebasemlcustom'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.4'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

def getFirebaseSDKVersion() {
  def firebaseCoreProject = findProject(':firebase_core')
  if (firebaseCoreProject == null) {
    return "26.8.0";
  }
  if (!rootProject.ext.has('FlutterFire')) return firebaseCoreProject.properties["FirebaseSDKVersion"]
  if (!rootProject.ext.get('FlutterFire')["FirebaseSDKVersion"]) return firebaseCoreProject.properties["FirebaseSDKVersion"]
  return rootProject.ext.get('FlutterFire').get("FirebaseSDKVersion")
}
def firebaseSDKVersion = getFirebaseSDKVersion();

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    dependencies {
        implementation platform("com.google.firebase:firebase-bom:${firebaseSDKVersion}")
        api 'com.google.firebase:firebase-ml-model-interpreter'
        implementation 'androidx.annotation:annotation:1.1.0'
        implementation 'androidx.exifinterface:exifinterface:1.3.1'
    }
}

apply from: file("./user-agent.gradle")
